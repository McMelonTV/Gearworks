plugins {
    id 'java'
}

group = 'ing.boykiss.gearworks'
version = '0.0.1-dev'

java {
    sourceCompatibility = JavaVersion.VERSION_25
    targetCompatibility = JavaVersion.VERSION_25
}

import org.gradle.internal.os.OperatingSystem

switch (OperatingSystem.current()) {
    case OperatingSystem.FREE_BSD:
        project.ext.lwjglNatives = "natives-freebsd"
        break
    case OperatingSystem.LINUX:
        project.ext.lwjglNatives = "natives-linux"
        def osArch = System.getProperty("os.arch")
        if (osArch.startsWith("arm") || osArch.startsWith("aarch64")) {
            project.ext.lwjglNatives += osArch.contains("64") || osArch.startsWith("armv8") ? "-arm64" : "-arm32"
        } else if (osArch.startsWith("ppc")) {
            project.ext.lwjglNatives += "-ppc64le"
        } else if (osArch.startsWith("riscv")) {
            project.ext.lwjglNatives += "-riscv64"
        }
        break
    case OperatingSystem.MAC_OS:
        project.ext.lwjglNatives = System.getProperty("os.arch").startsWith("aarch64") ? "natives-macos-arm64" : "natives-macos"
        break
    case OperatingSystem.WINDOWS:
        def osArch = System.getProperty("os.arch")
        project.ext.lwjglNatives = osArch.contains("64")
                ? "natives-windows${osArch.startsWith("aarch64") ? "-arm64" : ""}"
                : "natives-windows-x86"
        break
}

repositories {
    mavenCentral()

    maven {
        url "https://repo.spongepowered.org/maven/"
    }
    maven {
        url "https://maven.fabricmc.net/"
    }
}

configurations {
    loaderDeps {
        canBeResolved = true
        canBeConsumed = false
    }
}

dependencies {
    // Misc Dependencies
    compileOnly "org.projectlombok:lombok:1.18.42"
    annotationProcessor "org.projectlombok:lombok:1.18.42"

    implementation 'com.google.code.gson:gson:2.13.2'

    // Logging Dependencies
    implementation "org.slf4j:slf4j-api:2.0.17"
    implementation "ch.qos.logback:logback-core:1.5.27"
    implementation "ch.qos.logback:logback-classic:1.5.26"

    // LWJGL Dependencies
    implementation platform("org.lwjgl:lwjgl-bom:3.3.6")

    implementation "org.lwjgl:lwjgl"
    implementation "org.lwjgl:lwjgl-assimp"
    implementation "org.lwjgl:lwjgl-freetype"
    implementation "org.lwjgl:lwjgl-glfw"
    implementation "org.lwjgl:lwjgl-openal"
    implementation "org.lwjgl:lwjgl-opencl"
    implementation "org.lwjgl:lwjgl-opengl"
    implementation "org.lwjgl:lwjgl-opengles"
    implementation "org.lwjgl:lwjgl-stb"
    implementation "org.lwjgl:lwjgl-vulkan"
    implementation "org.lwjgl:lwjgl-zstd"
    implementation "org.lwjgl:lwjgl::$lwjglNatives"
    implementation "org.lwjgl:lwjgl-assimp::$lwjglNatives"
    implementation "org.lwjgl:lwjgl-freetype::$lwjglNatives"
    implementation "org.lwjgl:lwjgl-glfw::$lwjglNatives"
    implementation "org.lwjgl:lwjgl-openal::$lwjglNatives"
    implementation "org.lwjgl:lwjgl-opengl::$lwjglNatives"
    implementation "org.lwjgl:lwjgl-opengles::$lwjglNatives"
    implementation "org.lwjgl:lwjgl-stb::$lwjglNatives"
    if (lwjglNatives == "natives-macos" || lwjglNatives == "natives-macos-arm64") implementation "org.lwjgl:lwjgl-vulkan::$lwjglNatives"
    implementation "org.lwjgl:lwjgl-zstd::$lwjglNatives"
    implementation "org.joml:joml:1.10.8"
    implementation "org.joml:joml-primitives:1.10.0"

    // Fabric dependencies
    compileOnly "net.fabricmc:fabric-loader:0.18.4"
    loaderDeps "net.fabricmc:fabric-loader:0.18.4"
    compileOnly "net.fabricmc:tiny-mappings-parser:0.2.2.14"
    loaderDeps "net.fabricmc:tiny-mappings-parser:0.2.2.14"
    compileOnly "net.fabricmc:access-widener:2.1.0"
    loaderDeps "net.fabricmc:access-widener:2.1.0"

    implementation "com.google.guava:guava:33.5.0-jre"
    loaderDeps "com.google.guava:guava:33.5.0-jre"
    implementation "com.google.code.gson:gson:2.13.2"
    loaderDeps "com.google.code.gson:gson:2.13.2"

    // Mixin dependencies
    compileOnly "org.ow2.asm:asm:9.9.1"
    loaderDeps "org.ow2.asm:asm:9.9.1"
    compileOnly "org.ow2.asm:asm-analysis:9.9.1"
    loaderDeps "org.ow2.asm:asm-analysis:9.9.1"
    compileOnly "org.ow2.asm:asm-commons:9.9.1"
    loaderDeps "org.ow2.asm:asm-commons:9.9.1"
    compileOnly "org.ow2.asm:asm-tree:9.9.1"
    loaderDeps "org.ow2.asm:asm-tree:9.9.1"
    compileOnly "org.ow2.asm:asm-util:9.9.1"
    loaderDeps "org.ow2.asm:asm-util:9.9.1"
    compileOnly "org.spongepowered:mixin:0.8.5"
    loaderDeps "org.spongepowered:mixin:0.8.5"
}

jar {
    archiveBaseName = 'Gearworks'
    destinationDirectory = layout.buildDirectory.dir("client")

    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    from sourceSets.main.output
    from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }

    exclude 'META-INF/*.SF', 'META-INF/*.DSA', 'META-INF/*.RSA', 'META-INF/*.MF'
    exclude 'ing/boykiss/gearworks/AppGameProvider.class'
    exclude 'ing/boykiss/gearworks/AppGameProvider$*.class'
    exclude 'ing/boykiss/gearworks/AppGameTransformer.class'
    exclude 'ing/boykiss/gearworks/AppGameTransformer$*.class'
    exclude 'META-INF/services/net.fabricmc.loader.impl.game.GameProvider'
}


tasks.register('launcherJar', Jar) {
    manifest {
        attributes 'Main-Class': 'ing.boykiss.gearworks.launcher.LauncherMain'
    }
    archiveBaseName = 'GearworksLauncher'
    destinationDirectory = layout.buildDirectory.dir("client")
    dependsOn 'compileJava', 'processResources', ':launcher:classes'

    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    from(sourceSets.main.output.classesDirs) {
        include 'ing/boykiss/gearworks/AppGameProvider.class'
        include 'ing/boykiss/gearworks/AppGameProvider$*.class'
        include 'ing/boykiss/gearworks/AppGameTransformer.class'
        include 'ing/boykiss/gearworks/AppGameTransformer$*.class'
    }
    from(sourceSets.main.output.resourcesDir) {
        include 'META-INF/services/**'
    }
    from project(':launcher').sourceSets.main.output.classesDirs
    from { project(':launcher').configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
    from(configurations.loaderDeps.collect { it.isDirectory() ? it : zipTree(it) }) {
        exclude 'META-INF/services/net.fabricmc.loader.impl.game.GameProvider'
    }

    exclude 'META-INF/*.SF', 'META-INF/*.DSA', 'META-INF/*.RSA', 'META-INF/*.MF'
}

tasks.register('serverJar', Jar) {
    archiveBaseName = 'GearworksServer'
    destinationDirectory = layout.buildDirectory.dir("server")

    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    from sourceSets.main.output
    from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }

    exclude 'META-INF/*.SF', 'META-INF/*.DSA', 'META-INF/*.RSA', 'META-INF/*.MF'
    exclude 'ing/boykiss/gearworks/client/**'
    exclude 'ing/boykiss/gearworks/AppGameProvider.class'
    exclude 'ing/boykiss/gearworks/AppGameProvider$*.class'
    exclude 'ing/boykiss/gearworks/AppGameTransformer.class'
    exclude 'ing/boykiss/gearworks/AppGameTransformer$*.class'
    exclude 'META-INF/services/net.fabricmc.loader.impl.game.GameProvider'
}

tasks.register('serverLauncherJar', Jar) {
    manifest {
        attributes 'Main-Class': 'ing.boykiss.gearworks.launcher.ServerLauncherMain'
    }
    archiveBaseName = 'GearworksServerLauncher'
    destinationDirectory = layout.buildDirectory.dir("server")
    dependsOn 'compileJava', 'processResources', ':launcher:classes'

    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    from(sourceSets.main.output.classesDirs) {
        include 'ing/boykiss/gearworks/AppGameProvider.class'
        include 'ing/boykiss/gearworks/AppGameProvider$*.class'
        include 'ing/boykiss/gearworks/AppGameTransformer.class'
        include 'ing/boykiss/gearworks/AppGameTransformer$*.class'
    }
    from(sourceSets.main.output.resourcesDir) {
        include 'META-INF/services/**'
    }
    from project(':launcher').sourceSets.main.output.classesDirs
    from { project(':launcher').configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
    from(configurations.loaderDeps.collect { it.isDirectory() ? it : zipTree(it) }) {
        exclude 'META-INF/services/net.fabricmc.loader.impl.game.GameProvider'
    }

    exclude 'META-INF/*.SF', 'META-INF/*.DSA', 'META-INF/*.RSA', 'META-INF/*.MF'
}

tasks.register('buildAndCopyClient', Copy) {
    group 'build'
    dependsOn 'jar', 'launcherJar'
    from "build/client/"
    into "../run/client/vanilla/"
}

tasks.register('buildAndCopyServer', Copy) {
    group 'build'
    dependsOn 'serverJar', 'serverLauncherJar'
    from "build/server/"
    into "../run/server/vanilla/"
}