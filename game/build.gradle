plugins {
    id 'java'
}

group = 'ing.boykiss.gearworks'
version = '0.0.1-dev'

java {
    sourceCompatibility = JavaVersion.VERSION_25
    targetCompatibility = JavaVersion.VERSION_25
}

import org.gradle.internal.os.OperatingSystem

switch (OperatingSystem.current()) {
    case OperatingSystem.FREE_BSD:
        project.ext.lwjglNatives = "natives-freebsd"
        break
    case OperatingSystem.LINUX:
        project.ext.lwjglNatives = "natives-linux"
        def osArch = System.getProperty("os.arch")
        if (osArch.startsWith("arm") || osArch.startsWith("aarch64")) {
            project.ext.lwjglNatives += osArch.contains("64") || osArch.startsWith("armv8") ? "-arm64" : "-arm32"
        } else if (osArch.startsWith("ppc")) {
            project.ext.lwjglNatives += "-ppc64le"
        } else if (osArch.startsWith("riscv")) {
            project.ext.lwjglNatives += "-riscv64"
        }
        break
    case OperatingSystem.MAC_OS:
        project.ext.lwjglNatives = System.getProperty("os.arch").startsWith("aarch64") ? "natives-macos-arm64" : "natives-macos"
        break
    case OperatingSystem.WINDOWS:
        def osArch = System.getProperty("os.arch")
        project.ext.lwjglNatives = osArch.contains("64")
                ? "natives-windows${osArch.startsWith("aarch64") ? "-arm64" : ""}"
                : "natives-windows-x86"
        break
}

repositories {
    mavenCentral()

    maven {
        url "https://repo.spongepowered.org/maven/"
    }
    maven {
        url "https://maven.fabricmc.net/"
    }
}

dependencies {
    // Misc Dependencies
    compileOnly "org.projectlombok:lombok:1.18.42"
    annotationProcessor "org.projectlombok:lombok:1.18.42"

    implementation 'com.google.code.gson:gson:2.13.2'

    // Logging Dependencies
    implementation "org.slf4j:slf4j-api:2.0.17"
    implementation "ch.qos.logback:logback-core:1.5.27"
    implementation "ch.qos.logback:logback-classic:1.5.26"

    // LWJGL Dependencies
    implementation platform("org.lwjgl:lwjgl-bom:3.3.6")

    implementation "org.lwjgl:lwjgl"
    implementation "org.lwjgl:lwjgl-assimp"
    implementation "org.lwjgl:lwjgl-freetype"
    implementation "org.lwjgl:lwjgl-glfw"
    implementation "org.lwjgl:lwjgl-openal"
    implementation "org.lwjgl:lwjgl-opencl"
    implementation "org.lwjgl:lwjgl-opengl"
    implementation "org.lwjgl:lwjgl-opengles"
    implementation "org.lwjgl:lwjgl-stb"
    implementation "org.lwjgl:lwjgl-vulkan"
    implementation "org.lwjgl:lwjgl-zstd"
    implementation "org.lwjgl:lwjgl::$lwjglNatives"
    implementation "org.lwjgl:lwjgl-assimp::$lwjglNatives"
    implementation "org.lwjgl:lwjgl-freetype::$lwjglNatives"
    implementation "org.lwjgl:lwjgl-glfw::$lwjglNatives"
    implementation "org.lwjgl:lwjgl-openal::$lwjglNatives"
    implementation "org.lwjgl:lwjgl-opengl::$lwjglNatives"
    implementation "org.lwjgl:lwjgl-opengles::$lwjglNatives"
    implementation "org.lwjgl:lwjgl-stb::$lwjglNatives"
    if (lwjglNatives == "natives-macos" || lwjglNatives == "natives-macos-arm64") implementation "org.lwjgl:lwjgl-vulkan::$lwjglNatives"
    implementation "org.lwjgl:lwjgl-zstd::$lwjglNatives"
    implementation "org.joml:joml:1.10.8"
    implementation "org.joml:joml-primitives:1.10.0"

    // Fabric dependencies
    implementation "net.fabricmc:fabric-loader:0.18.4"
    implementation "net.fabricmc:tiny-mappings-parser:0.2.2.14"
    implementation "net.fabricmc:access-widener:2.1.0"

    implementation "com.google.guava:guava:33.5.0-jre"
    implementation "com.google.code.gson:gson:2.13.2"

    // Mixin dependencies
    implementation "org.ow2.asm:asm:9.9.1"
    implementation "org.ow2.asm:asm-analysis:9.9.1"
    implementation "org.ow2.asm:asm-commons:9.9.1"
    implementation "org.ow2.asm:asm-tree:9.9.1"
    implementation "org.ow2.asm:asm-util:9.9.1"
    implementation "org.spongepowered:mixin:0.8.5"
}

jar {
    manifest {
        attributes 'Main-Class': 'net.fabricmc.loader.impl.launch.knot.KnotClient'
    }
    archiveBaseName = 'Gearworks'
    destinationDirectory = layout.buildDirectory.dir("client")

    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    from sourceSets.main.output
    from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }

    exclude 'META-INF/*.SF', 'META-INF/*.DSA', 'META-INF/*.RSA', 'META-INF/*.MF'
}

tasks.register('serverJar', Jar) {
    manifest {
        attributes 'Main-Class': 'net.fabricmc.loader.impl.launch.knot.KnotServer'
    }
    archiveBaseName = 'GearworksServer'
    destinationDirectory = layout.buildDirectory.dir("server")

    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    from sourceSets.main.output
    from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }

    exclude 'META-INF/*.SF', 'META-INF/*.DSA', 'META-INF/*.RSA', 'META-INF/*.MF'
    exclude 'ing/boykiss/gearworks/client/**'
}

tasks.register('buildAndCopyClient', Copy) {
    group 'build'
    dependsOn 'jar'
    from "build/client/"
    into "../runClient/"
}

tasks.register('buildAndCopyServer', Copy) {
    group 'build'
    dependsOn 'serverJar'
    from "build/server/"
    into "../runServer/"
}